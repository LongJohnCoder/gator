space coord model is vec<3>;
space coord world is vec<3>;
tag viewport is vec<3>;
tag camera is viewport;
tag light is viewport;
space coord view<`t : viewport> is `t;
tag coord proj<`t : viewport> is `t;
tag coord hom<`t : vec<n>> is vec<n+1>;
tag dir<`t : vec<n>> is vec<n>;
space color is vec<3>;
tag alpha<`t : color> is vec<4>;

declare `t texture2D<`t : vec<4>>(sampler2D<`t> texture, vec2 cor);
declare dir<`t> normalize<`t : vec>(`t x);
declare float dot<`t: vec>(`t v1, `t v2);
declare vec4 vec4<`t : vec<3>>(`t v, float f);
declare vec3 vec3<`t : vec<4>>(`t v);
declare float min(float f1, float f2);
declare float max(float f1, float f2);
declare `t reflect<`t : vec>(`t v1, `t v2);
declare float pow(float f1, float f2);
declare float length<`t : vec>(`t v);

uniform sampler2D<alpha<color>> uTexture;
varying vec3 vShadowPos;
varying model vPosition;
uniform hom<model>->hom<world> uModel;
uniform hom<world>->hom<view<camera>> uCamera;
varying dir<model> vNormal;
uniform world uLight;
uniform color uBaseColor;
uniform float uSpecStrength;

canon hom<`t> homify<`t : vec<3>>(`t v) { return vec4(v, 1.) as hom<`t>; }
canon `t hom_reduce<`t : vec<3>>(hom<`t> v) { return vec3(v) as `t; }
canon hom<`t> homify_normal<`t : vec<3>>(dir<`t> v) { return vec4(v, 0.) as hom<`t>; }
canon alpha<color> extendColor(color v) { return vec4(v, 1.) as alpha<color>; }

float decodeFloat (alpha<color> c) {
  alpha<color> bitShift = [1.0 / (256. * 256. * 256.), 1.0 / (256. * 256.), 1.0 / (256.0), 1.];
  return dot(c, bitShift);
}

color phong_light<`t: vec<3>>(`t lightPos, `t fragPos, dir<`t> normalDir, color baseColor,
float specStrength, float linear, float quad) {
    float lambertian = max(0., dot(normalDir, normalize<`t>(lightPos)));
    dir<`t> reflectDir = reflect(-normalize(lightPos in `t), normalDir);
    float specular = 0.;
    if (0. <= lambertian) {
        specular = pow(max(0., dot(-normalize(fragPos in `t), reflectDir)), 32.);
    }

    float distance = length(lightPos - fragPos);
    float attenuation = 1.0 / (linear * distance + quad * distance * distance); 
    return (lambertian * baseColor + specStrength * specular * [1., 1., 1.]) * attenuation;
}

void main() {
  // vec4 color = [.6, .5, .2, 1.];
  color ambient = [.1, 0., 0.];
  float texelSize = 1. / 1024.;
  alpha<color> v = texture2D(uTexture, vShadowPos.xy);
  float amountInLight = 0.;
  for (int x = -1; x <= 1; x++) {
    for (int y = -1; y <= 1; y++) {
      float texelDepth = decodeFloat(texture2D(uTexture, vShadowPos.xy + [x, y] * texelSize));
      // float texelDepth = texture2D(uTexture, vShadowPos.xy + [x, y] * texelSize).z;
      if (vShadowPos.z - 0.007 <= texelDepth) {
        amountInLight += 1.0;
      }
    }
  }
  amountInLight /= 9.;
  color phong_color = phong_light(uLight, vPosition in world, normalize(vNormal in world), 
    uBaseColor, uSpecStrength, .08, .0001);

  alpha<color> gl_FragColor = extendColor(ambient + phong_color * amountInLight);
}