type vec3 is float[3];
type vec4 is float[4];
type mat3 is float[3][3];
type mat4 is float[4][4];
type scalar is float;
with float F: type test is F;
with float Q: type test2 is test<Q>;

with vec3 T: declare T +(T v1, T v2);
with vec3 T: declare T -(T v1, T v2);
with vec3 T: declare T -(T v);
with vec3 T: with float U: declare T *(T v, U f);
with vec3 T: with float U: declare T *(U f, T v);
with vec3 T: with float U: declare T /(T f1, U f2);

with vec4 T: declare T +(T v1, T v2);
with vec4 T: declare T -(T v1, T v2);
with vec4 T: declare T -(T v);
with vec4 T: with float U: declare T *(T v, U f);
with vec4 T: with float U: declare T *(U f, T v);

with float T: declare T +(T f1, T f2);
with float T: declare T -(T f1, T f2);
with float T: declare T -(T f);
with float T: declare T *(T f1, T f2);
with float T: declare T /(T f1, T f2);

with mat3 T: with vec3 U: declare vec3 *(T m, U v);
with mat3 T: with mat3 U: declare mat3 +(T m, U v);
with mat3 T: with mat3 U: declare mat3 *(T m, U v);

with mat4 T: with vec4 U: declare vec4 *(T m, U v);
with mat4 T: with mat4 U: declare mat4 +(T m, U v);
with mat4 T: with mat4 U: declare mat4 *(T m, U v);

declare float cos(float f);
declare float sqrt(float f);
declare float acos(float f);

with float[3] T: with float U: declare vec4 vec4(T v, U f);
with float[4] T: declare vec3 vec3(T v);

with float T: declare test2<T> f(test2<T> x);

prototype geometry {
    object point;
    object vector;
    object direction;
    with frame() r: object matrix;

    vector +(vector x, vector y);
    vector -(vector x, vector y);
    vector -(vector x);
    vector *(vector v, scalar s);
    vector *(scalar s, vector v);
    point translate(point p, vector v);
    point +(point p, vector v);
    point +(vector p, point v);
    vector -(point x, point y);
    vector -(point x);
    with frame() target:
    this<target>.vector *(matrix<target> m, vector v);

    with frame() target:
    this<target>.vector *(matrix<target> m, direction d);

    with frame() target:
    this<target>.point *(matrix<target> m, point p);

    with frame() target:
    matrix<target> +(matrix<target> m1, matrix<target> m2);

    with frame() middle, target:
    matrix<target> *(matrix<middle> m1, this<middle>.matrix<target> m2);
}

with frame(3) r:
coordinate cart3 : geometry {
    object point is float[3];
    object vector is float[3];
    object direction is float[3];
    with frame(3) r2: object matrix is float[3][3];

    vector +(vector x, vector y) {
        return (x as! vec3 + y as! vec3) as! vector;
    }
    vector -(vector x, vector y) {
        return (x as! vec3 - y as! vec3) as! vector;
    }
    vector *(vector v, scalar s) {
        return (v as! vec3 * s) as! vector;
    }
    vector *(scalar s, vector v) {
        return (s * v as! vec3) as! vector;
    }
    vector -(vector v) {
        return (-v as! vec3) as! vector;
    }
    point translate(point p, vector v) {
        return (p as! vec3 + v as! vec3) as! point;
    }
    point +(point p, vector v) {
        return (p as! vec3 + v as! vec3) as! point;
    }
    point +(vector v, point p) {
        return (p as! vec3 + v as! vec3) as! point;
    }
    vector -(point x, point y) {
        return (x as! vec3 - y as! vec3) as! vector;
    }
    vector -(point v) {
        return (-v as! vec3) as! vector;
    }
    with frame(3) target:
    this<target>.vector *(matrix<target> m, vector v) {
        return (m as! mat3 * v as! vec3) as! this<target>.vector;
    }
    with frame(3) target:
    this<target>.vector *(matrix<target> m, direction d) {
        return (m as! mat3 * d as! vec3) as! this<target>.vector;
    }
    with frame(3) target:
    this<target>.point *(matrix<target> m, point p) {
        return (m as! mat3 * p as! vec3) as! this<target>.point;
    }
    with frame(3) target:
    matrix<target> +(matrix<target> m1, matrix<target> m2) {
        return (m1 as! mat3 + m2 as! mat3) as! matrix<target>;
    }
    with frame(3) middle, target:
    matrix<target> *(matrix<middle> m1, this<middle>.matrix<target> m2) {
        return (m1 as! mat3 * m2 as! mat3) as! matrix<target>;
    }
}

with frame(3) r:
coordinate hom : geometry {
    object point is float[4];
    object vector is float[4];
    object direction is float[4];
    with frame(3) r2: object matrix is float[4][4];

    point +(point p, vector v) {
        return (p as! vec4 + v as! vec4 * p[3]) as! point;
    }
    point +(vector v, point p) {
        return (p as! vec4 + v as! vec4 * p[3]) as! point;
    }
    vector -(point x, point y) {
        return (x as! vec4 * y[3] - y as! vec4 * x[3]) as! vector;
    }
    with frame(3) target:
    this<target>.vector *(matrix<target> m, vector v) {
        return (m as! mat4 * v as! vec4) as! this<target>.vector;
    }
    with frame(3) target:
    this<target>.vector *(matrix<target> m, direction d) {
        return (m as! mat4 * d as! vec4) as! this<target>.vector;
    }
    with frame(3) target:
    this<target>.point *(matrix<target> m, point p) {
        return (m as! mat4 * p as! vec4) as! this<target>.point;
    }
    with frame(3) target:
    matrix<target> +(matrix<target> m1, matrix<target> m2) {
        return (m1 as! mat4 + m2 as! mat4) as! matrix<target>;
    }
    with frame(3) middle, target:
    matrix<target> *(matrix<middle> m1, this<middle>.matrix<target> m2) {
        return (m1 as! mat4 * m2 as! mat4) as! matrix<target>;
    }
}

frame model has dimension 3;
frame world has dimension 3;

with frame(3) q:
hom<q>.point homify(cart3<q>.point v) {
    return vec4(v, 1.) as! hom<q>.point;
}
with frame(3) r:
hom<r>.vector homify(cart3<r>.vector v) {
    return vec4(v, 1.) as! hom<r>.vector;
}
with frame(3) r:
cart3<r>.point reduce_hom(hom<r>.point v) {
    return (vec3(v) / v[3]) as! cart3<r>.point;
}
with frame(3) r:
cart3<r>.vector reduce_hom(hom<r>.vector v) {
    return vec3(v) as! cart3<r>.vector;
}

void main() {
    cart3<model>.point pos = [1., 2., 3.];
    cart3<world>.point light = [-1., -1., 0.];
    cart3<world>.vector offset = [2., 2., 2.];

    // Pure translation matrix
    hom<model>.matrix<world> mtrans = 
        [[1., 0., 0., 1.], 
         [0., 1., 0., -1.],
         [0., 0., 1., 1.],
         [0., 0., 0., 1.]];

    hom<world>.matrix<model> minv = 
        [[1., 0., 0., -1.], 
         [0., 1., 0., 1.],
         [0., 0., 1., -1.],
         [0., 0., 0., 1.]];

    hom<model>.matrix<model> mid = mtrans * minv;

    hom<model>.point hpos = homify<model>(pos);
    hom<world>.vector hoff = homify<world>(offset);

    print hpos;
    print hoff;

    print (mtrans * hpos) + hoff;
    print mtrans * (hpos + (minv * hoff));  // Should be the same!
    print reduce_hom<world>(homify<world>(light) + homify<world>(offset));
}